SUBROUTINE DUMP.DYNAMIC.ARRAY(DUMP.ARRAY,ARRAY.NAME,FILE.NAME, FIELD.SIZE)
   EQU VM TO CHAR(253)
   EQU AM TO CHAR(254)
   EQU SVM TO CHAR(252)
   EQU TRUE TO 1
   EQU FALSE TO 0
   OPEN "DICT",FILE.NAME TO DICT.FILE ELSE PRINT "CANNOT OPEN DICT " : FILE.NAME ; ABORT = 1
   FIELD.LIMIT = "L#":FIELD.SIZE
   PADDING = 8  ;* COUNT OF EXTRA CHARACTERS BETWEEN |  |
   HOW.MANY.COLUMNS = 3
   LINE.CTR=0
   DASH.ROW = '+' : STR('-',FIELD.SIZE*HOW.MANY.COLUMNS + PADDING) : '+'
   HEADER = '| ' : "ATTRIBUTE NUMBER" FIELD.LIMIT : ' | ' : "VALUE(S)/SUBVALUE(S)" FIELD.LIMIT : ' | ' : "DICTIONARY" FIELD.LIMIT : ' |'
   PRINT
   PRINT DASH.ROW
   PRINT HEADER
   PRINT DASH.ROW
   DUMP.CNT = DCOUNT(DUMP.ARRAY,AM)
   LINE.CTR=1
   
   FOR I=1 TO DUMP.CNT
      VM.CNT = DCOUNT(DUMP.ARRAY<I>,VM)
      RESPONSE="" 
      FOR J=1 TO VM.CNT
         SVM.CNT = DCOUNT(DUMP.ARRAY<I,J>,SVM) 
         ATTRIBUTE.NUMBER = ARRAY.NAME : '<': I : ',' : J : '>'
         
         EOF=FALSE
         DICT.CTR=0
         STMT = "SELECT DICT " : FILE.NAME : ' WITH *a1 EQ "A" AND WITH *a2 EQ ' : I : ' AND WITH *a0 NE ".]"'
         EXECUTE STMT CAPTURING OUTPUT
         DICT.CNT = FIELD(OUTPUT,' ',2)
         IF DICT.CNT GT 0 THEN
            GOSUB HAS.DICTS
         END ELSE
            PRINT '| ' : ATTRIBUTE.NUMBER FIELD.LIMIT : ' | ' : DUMP.ARRAY<I,J> FIELD.LIMIT : ' | ' : "" FIELD.LIMIT : ' |' 
            GOSUB WANT.MORE
         END
      NEXT J
      IF RESPONSE EQ 'C' THEN EXIT
   NEXT I
   PRINT DASH.ROW
   PRINT
RETURN

HAS.DICTS:
   LOOP
      READNEXT DICT.NAME ELSE EOF=TRUE
   UNTIL EOF DO
      READV CONVERSION FROM DICT.FILE,DICT.NAME,7
      IF CONVERSION NE "" THEN
         DICT.NAME = DICT.NAME : ' --> ' : CONVERSION
      END
      DICT.CTR = DICT.CTR +1
      IF DICT.CTR = 1 THEN
         PRINT '| ' : ATTRIBUTE.NUMBER FIELD.LIMIT : ' | ' : DUMP.ARRAY<I,J> FIELD.LIMIT : ' | ' : DICT.NAME FIELD.LIMIT : ' |'
      END ELSE
         PRINT '| ' : "" FIELD.LIMIT : ' | ' : "" FIELD.LIMIT : ' | ' : DICT.NAME FIELD.LIMIT : ' |'
      END  
      GOSUB WANT.MORE
   REPEAT
   GOSUB WANT.MORE
RETURN

WANT.MORE:
   LINE.CTR = LINE.CTR + 1
   IF MOD(LINE.CTR,20) = 0 THEN
      LOOP
         PRINT
         PRINT "ENTER FOR MORE. C TO CANCEL. O TO CONVERT " : 
         INPUT RESPONSE
         IF RESPONSE EQ 'C' THEN EXIT
         IF RESPONSE EQ 'O' THEN
            PRINT "Attribute number*Conversion: " : 
            INPUT NUMBERANDCONVERSION
            ATTR.NUMBER             = TRIM(FIELD(NUMBERANDCONVERSION,'*',1))
            CONVERSION              = TRIM(FIELD(NUMBERANDCONVERSION,'*',2))
            OCONV.STRING            = "OCONV(" : DUMP.ARRAY<ATTR.NUMBER> : ",'" : CONVERSION : "')"
            CONVERTED.VALUE         = OCONV(DUMP.ARRAY<ATTR.NUMBER>,CONVERSION)
            PRINT OCONV.STRING : ' = ' : CONVERTED.VALUE :
         END
      WHILE RESPONSE NE "" DO REPEAT
      PRINT
   END
RETURN
